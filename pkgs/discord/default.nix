{
  pkgs,
  discordSrc,
  vencordSrc,
}:

let
  lib = pkgs.lib;

  # Read the PNPM hash generated by Github Actions
  pnpmHash = lib.fileContents ./vencord-pnpm-hash.txt;

  # Extract Discord to strip root permissions from chrome-sandbox
  extractedDiscord = pkgs.runCommand "extracted-discord" { } ''
    mkdir $out
    tar -xf ${discordSrc.src} -C $out --strip-components=1 --no-same-permissions --no-same-owner
  '';

  vencord-git = pkgs.vencord.overrideAttrs (old: {
    pname = "vencord-git";
    version = vencordSrc.version;
    src = vencordSrc.src;

    # Override the pnpmDeps with the new hash and source
    pnpmDeps = pkgs.fetchPnpmDeps {
      pname = "vencord-git";
      version = vencordSrc.version;
      src = vencordSrc.src;
      pnpm = pkgs.pnpm_10;
      fetcherVersion = 2;
      hash = pnpmHash;
    };

    env = {
      VENCORD_REMOTE = "Vendicated/Vencord";
      VENCORD_HASH = vencordSrc.version;
    };

    passthru = { };
  });

  discord-stable = pkgs.discord.override {
    version = discordSrc.version;
    src = extractedDiscord;
    withVencord = false;
    vencord = vencord-git;
  };

in

# Return packages separately
{
  discord = discord-stable;
  vencord = vencord-git;
}
