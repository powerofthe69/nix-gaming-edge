{
  pkgs,
  discordSrc,
  vencordSrc,
}:

let
  lib = pkgs.lib;

  # Read the PNPM hash generated by Github Actions
  pnpmHash = lib.fileContents ./vencord-pnpm-hash.txt;

  vencord-git = pkgs.vencord.overrideAttrs (old: {
    pname = "vencord-git";
    version = vencordSrc.version;
    src = vencordSrc.src;

    # Override the pnpmDeps with the new hash and source
    pnpmDeps = pkgs.fetchPnpmDeps {
      pname = "vencord-git";
      version = vencordSrc.version;
      src = vencordSrc.src;
      pnpm = pkgs.pnpm_10;
      fetcherVersion = 2;
      hash = pnpmHash;
    };

    env = {
      VENCORD_REMOTE = "Vendicated/Vencord";
      VENCORD_HASH = vencordSrc.version;
    };

    passthru = { };
  });

  discord-stable = pkgs.discord.override {
    version = discordSrc.version;
    src = discordSrc.src;
    withVencord = false;
  };

in

# Return packages separately
{
  discord = discord-stable;
  vencord = vencord-git;
}
