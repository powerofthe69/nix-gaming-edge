{
  description = "A convenient gaming flake for NixOS";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
  };

  nixConfig = {
    extra-substituters = [ "https://nix-cache.tokidoki.dev/tokidoki" ];
    extra-trusted-public-keys = [
      "tokidoki:MD4VWt3kK8Fmz3jkiGoNRJIW31/QAm7l1Dcgz2Xa4hk="
    ];
  };

  outputs =
    { self, nixpkgs }:
    let
      system = "x86_64-linux";
      pkgs = import nixpkgs {
        inherit system;
        config.allowUnfree = true;
      };

      # sources as generated by nvfetcher -o _sources
      nvSources = pkgs.callPackage ./_sources/generated.nix { };

      # Mesa-git
      makeMesaPkgs =
        pkgs':
        import ./pkgs/mesa-git {
          pkgs = pkgs';
          mesa-src = nvSources.mesa.src // {
            rev = nvSources.mesa.version;
          };
          libdrm-src = nvSources.libdrm.src // {
            rev = nvSources.libdrm.version;
          };
          wayland-protocols-src = nvSources.wayland-protocols.src // {
            rev = nvSources.wayland-protocols.version;
          };
        };
      mesaPkgs = makeMesaPkgs pkgs;

      mkProton =
        sourceKey: variantName:
        pkgs.callPackage ./pkgs/proton-cachyos {
          source = nvSources.${sourceKey};
          variant = variantName;
        };

      # Remove URL prefix from latest version string
      mkVintageStory =
        sourceKey:
        let
          src = nvSources.${sourceKey};

          version =
            let
              v = src.version;
            in
            if builtins.match ".*vs_client_linux-x64_(.*)" v != null then
              builtins.elemAt (builtins.match ".*vs_client_linux-x64_(.*)" v) 0
            else
              v;
        in
        pkgs.callPackage ./pkgs/vintagestory {
          sourceData = {
            inherit version;
            inherit (src) src;
          };
        };

    in
    {
      packages.${system} = {
        # Mesa-git
        mesa-git = mesaPkgs.mesa-git;
        mesa32-git = mesaPkgs.mesa32-git;
        libdrm-git = mesaPkgs.libdrm-git;
        wayland-protocols-git = mesaPkgs.wayland-protocols-git;

        # Proton CachyOS
        proton-cachyos = mkProton "proton-cachyos" "base";
        proton-cachyos-x86_64_v2 = mkProton "proton-cachyos-x86_64_v2" "x86_64_v2";
        proton-cachyos-x86_64_v3 = mkProton "proton-cachyos-x86_64_v3" "x86_64_v3";
        proton-cachyos-x86_64_v4 = mkProton "proton-cachyos-x86_64_v4" "x86_64_v4";

        # Vintage Story
        vintagestory = mkVintageStory "vintagestory";
        vintagestory-stable = mkVintageStory "vintagestory-stable";

        # PokeMMO
        pokemmo = pkgs.callPackage ./pkgs/pokemmo {
          inherit (nvSources.pokemmo) src;
        };

        # Pseudoregalia Randomizer
        pseudoregalia-rando = pkgs.callPackage ./pkgs/pseudoregalia-rando {
          inherit (nvSources.pseudoregalia-rando) src version;
          oodleSrc = nvSources.oodle-lib.src;
        };
      };

      overlays = {
        default =
          final: prev:
          let
            mp = makeMesaPkgs final;
          in
          {
            inherit (mp)
              mesa-git
              mesa32-git
              libdrm-git
              wayland-protocols-git
              ;
            proton-cachyos = self.packages.${system}.proton-cachyos;
            proton-cachyos-x86_64_v2 = self.packages.${system}.proton-cachyos-x86_64_v2;
            proton-cachyos-x86_64_v3 = self.packages.${system}.proton-cachyos-x86_64_v3;
            proton-cachyos-x86_64_v4 = self.packages.${system}.proton-cachyos-x86_64_v4;
            vintagestory = self.packages.${system}.vintagestory;
            vintagestory-stable = self.packages.${system}.vintagestory-stable;
            pokemmo = self.packages.${system}.pokemmo;
            pseudoregalia-rando = self.packages.${system}.pseudoregalia-rando;
          };

        mesa-git =
          final: prev:
          let
            mp = makeMesaPkgs final;
          in
          {
            inherit (mp)
              mesa-git
              mesa32-git
              libdrm-git
              wayland-protocols-git
              ;
          };

        proton-cachyos = final: prev: {
          proton-cachyos = self.packages.${system}.proton-cachyos;
          proton-cachyos-x86_64_v2 = self.packages.${system}.proton-cachyos-x86_64_v2;
          proton-cachyos-x86_64_v3 = self.packages.${system}.proton-cachyos-x86_64_v3;
          proton-cachyos-x86_64_v4 = self.packages.${system}.proton-cachyos-x86_64_v4;
        };

        vintagestory = final: prev: {
          vintagestory = self.packages.${system}.vintagestory;
          vintagestory-stable = self.packages.${system}.vintagestory-stable;
        };

        pokemmo = final: prev: {
          pokemmo = self.packages.${system}.pokemmo;
        };

        pseudoregalia-rando = final: prev: {
          pseudoregalia-rando = self.packages.${system}.pseudoregalia-rando;
        };
      };

      nixosModules = {
        mesa-git = import ./modules/mesa-git.nix;
        default = self.nixosModules.mesa-git;
      };
    };
}
