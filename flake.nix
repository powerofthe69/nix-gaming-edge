{
  description = "A convenient gaming flake for NixOS";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
  };

  nixConfig = {
    extra-substituters = [ "https://nix-cache.tokidoki.dev/tokidoki" ];
    extra-trusted-public-keys = [
      "tokidoki:MD4VWt3kK8Fmz3jkiGoNRJIW31/QAm7l1Dcgz2Xa4hk="
    ];
  };

  outputs =
    { self, nixpkgs }:
    let
      system = "x86_64-linux";
      pkgs = import nixpkgs {
        inherit system;
        config.allowUnfree = true;
      };

      # sources as generated by nvfetcher -o _sources
      nvSources = pkgs.callPackage ./_sources/generated.nix { };

      # Mesa-git
      makeMesaPkgs =
        pkgs':
        import ./pkgs/mesa-git {
          pkgs = pkgs';
          mesa-src = nvSources.mesa.src // {
            rev = nvSources.mesa.version;
          };
          libdrm-src = nvSources.libdrm.src // {
            rev = nvSources.libdrm.version;
          };
          wayland-protocols-src = nvSources.wayland-protocols.src // {
            rev = nvSources.wayland-protocols.version;
          };
        };
      mesaPkgs = makeMesaPkgs pkgs;

      mkProton =
        sourceKey: variantName:
        pkgs.callPackage ./pkgs/proton-cachyos {
          source = nvSources.${sourceKey};
          variant = variantName;
        };

      # Remove URL prefix from latest version string
      mkVintageStory =
        sourceKey:
        let
          src = nvSources.${sourceKey};

          version =
            let
              v = src.version;
            in
            if builtins.match ".*vs_client_linux-x64_(.*)" v != null then
              builtins.elemAt (builtins.match ".*vs_client_linux-x64_(.*)" v) 0
            else
              v;
        in
        pkgs.callPackage ./pkgs/vintagestory {
          sourceData = {
            inherit version;
            inherit (src) src;
          };
        };

      proton = mkProton "proton-cachyos" "base";
      protonv2 = mkProton "proton-cachyos-x86_64-v2" "x86_64-v2";
      protonv3 = mkProton "proton-cachyos-x86_64-v3" "x86_64-v3";
      protonv4 = mkProton "proton-cachyos-x86_64-v4" "x86_64-v4";

    in
    {
      packages.${system} = {
        # Mesa-git
        mesa-git = mesaPkgs.mesa-git;
        mesa32-git = mesaPkgs.mesa32-git;
        libdrm-git = mesaPkgs.libdrm-git;
        libdrm32-git = mesaPkgs.libdrm32-git;
        wayland-protocols-git = mesaPkgs.wayland-protocols-git;

        # Proton CachyOS
        proton-cachyos = proton;
        proton-cachyos-x86_64-v2 = protonv2;
        proton-cachyos-x86_64-v3 = protonv3;
        proton-cachyos-x86_64-v4 = protonv4;

        # Deprecated aliases (w/ underscores) for backwards compatibility
        proton-cachyos-x86_64_v2 = protonv2;
        proton-cachyos-x86_64_v3 = protonv3;
        proton-cachyos-x86_64_v4 = protonv4;

        # Vintage Story
        vintagestory = mkVintageStory "vintagestory";
        vintagestory-stable = mkVintageStory "vintagestory-stable";

        # PokeMMO
        pokemmo = pkgs.callPackage ./pkgs/pokemmo {
          inherit (nvSources.pokemmo) src;
        };

        # Pseudoregalia Randomizer
        pseudoregalia-rando = pkgs.callPackage ./pkgs/pseudoregalia-rando {
          inherit (nvSources.pseudoregalia-rando) src version;
          oodleSrc = nvSources.oodle-lib.src;
        };
      };

      overlays = {
        default = final: prev: {
          mesa-git = self.packages.${final.system}.mesa-git;
          mesa32-git = self.packages.${final.system}.mesa32-git;
          libdrm-git = self.packages.${final.system}.libdrm-git;
          libdrm32-git = self.packages.${final.system}.libdrm32-git;
          wayland-protocols-git = self.packages.${final.system}.wayland-protocols-git;

          proton-cachyos = self.packages.${final.system}.proton-cachyos;
          proton-cachyos-x86_64-v2 = self.packages.${final.system}.proton-cachyos-x86_64-v2;
          proton-cachyos-x86_64-v3 = self.packages.${final.system}.proton-cachyos-x86_64-v3;
          proton-cachyos-x86_64-v4 = self.packages.${final.system}.proton-cachyos-x86_64-v4;

          vintagestory = self.packages.${final.system}.vintagestory;
          vintagestory-stable = self.packages.${final.system}.vintagestory-stable;

          pokemmo = self.packages.${final.system}.pokemmo;

          pseudoregalia-rando = self.packages.${final.system}.pseudoregalia-rando;
        };

        mesa-git = final: prev: {
          mesa-git = self.packages.${final.system}.mesa-git;
          mesa32-git = self.packages.${final.system}.mesa32-git;
          libdrm-git = self.packages.${final.system}.libdrm-git;
          libdrm32-git = self.packages.${final.system}.libdrm32-git;
          wayland-protocols-git = self.packages.${final.system}.wayland-protocols-git;
        };

        proton-cachyos = final: prev: {
          proton-cachyos = self.packages.${final.system}.proton-cachyos;
          proton-cachyos-x86_64-v2 = self.packages.${final.system}.proton-cachyos-x86_64-v2;
          proton-cachyos-x86_64-v3 = self.packages.${final.system}.proton-cachyos-x86_64-v3;
          proton-cachyos-x86_64-v4 = self.packages.${final.system}.proton-cachyos-x86_64-v4;

          # Deprecated aliases w/ underscores
          proton-cachyos-x86_64_v2 = self.packages.${final.system}.proton-cachyos-x86_64-v2;
          proton-cachyos-x86_64_v3 = self.packages.${final.system}.proton-cachyos-x86_64-v3;
          proton-cachyos-x86_64_v4 = self.packages.${final.system}.proton-cachyos-x86_64-v4;
        };

        vintagestory = final: prev: {
          vintagestory = self.packages.${final.system}.vintagestory;
          vintagestory-stable = self.packages.${final.system}.vintagestory-stable;
        };

        pokemmo = final: prev: {
          pokemmo = self.packages.${final.system}.pokemmo;
        };

        pseudoregalia-rando = final: prev: {
          pseudoregalia-rando = self.packages.${final.system}.pseudoregalia-rando;
        };
      };

      nixosModules = {
        mesa-git = import ./modules/mesa-git.nix;
        default = self.nixosModules.mesa-git;
      };
    };
}
